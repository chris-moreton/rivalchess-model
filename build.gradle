buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id "org.sonarqube" version "2.8"
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
}

group = "com.netsensia.rivalchess"
version = '7.0.4'

jar.baseName("rivalchess-model")

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'rivalchess-model'
                packaging 'jar'
                description 'The simple model classes used by Rival Chess'
                url 'https://github.com/chris-moreton/rivalchess-model'

                scm {
                    connection = 'scm:git:git://github.com/chris-moreton/rivalchess-model.git'
                    developerConnection = 'scm:git:ssh://github.com/chris-moreton/rivalchess-model.git'
                    url = 'https://github.com/chris-moreton/rivalchess-model'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'chris-moreton'
                        name = 'Chris Moreton'
                        email = 'chris@netsensia.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.30'

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

}

test {
    useJUnit()
    maxHeapSize = '1G'
    testLogging.showStandardStreams = true
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
